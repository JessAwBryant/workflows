"""
Collection of rules from setting up AnVio profiles
"""

config.setdefault('anvio_env','anvio')
config.setdefault('samples_name','all')
config['merged_samples_dir'] = "anvio/samples-{}-merged"\
                                    .format(config['samples_name'])
config['merged_samples_status'] = "anvio/status/samples-{}-merged"\
                                    .format(config['samples_name'])
config.setdefault('outputs',set()).add(config['merged_samples_status'])

include: "../common/mapping_bwa.snake"

rule anvio_profile:
    input:
        "anvio/status/contig-db",
        "mapping/{sample}.reads.vs.contigs.bam.bai"
    output:
        "anvio/profile-{sample}/RUNINFO.cp"
    log:
        "logs/anvio-profile-{sample}"
    benchmark:
        "benchmarks/anvio-profile-{sample}.time"
    params:
        bam=lambda w: "mapping/{sample}.reads.vs.contigs.bam".format(sample=w.sample),
        min_len=config.get("min_contig_length",2500)
    version:
        get_version("anvi-profile",
                    cmd_prefix="source activate {}\n"\
                               .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """source activate {config[anvio_env]}
           rm -rf anvio/profile-{wildcards.sample}
           anvi-profile -i {params.bam} -c anvio/contigs.db -S {wildcards.sample} \
            -o anvio/profile-{wildcards.sample} \
            --min-contig-length {params.min_len}"""

rule anvio_merge_profiles:
    """ Merge all the AnVio profiles """
    input:
        contig_status="anvio/status/contig-db",
        profiles=lambda w: expand("anvio/profile-{sample}/RUNINFO.cp",
                            sample=config['reads']),
    output:
        config['merged_samples_status']
    log:
        "logs/anvio-merged"
    benchmark:
        "benchmarks/anvio.merge.time"
    version:
        get_version("anvi-profile",
                    cmd_prefix="source activate {}\n"\
                               .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """source activate {config[anvio_env]}
           rm -rf {config[merged_samples_dir]}
           anvi-merge {input.profiles} -c anvio/contigs.db \
                -o {config[merged_samples_dir]}
           touch {output}"""

