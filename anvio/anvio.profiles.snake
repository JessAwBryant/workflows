"""
Collection of rules from setting up AnVio profiles
"""

config.setdefault('anvio_env','anvio')
config.setdefault('samples_name','all')
merged_samples_dir = "anvio/status/samples-{}-merged"\
                                    .format(config['samples_name'])
config.setdefault('outputs',set()).add(merged_samples_dir)

rule anvio_profile:
    input:
        "anvio/status/contig-db",
        "mapping/{sample}.reads.vs.contigs.bam.bai"
    output:
        "anvio/profile-{sample}/RUNINFO.cp"
    log:
        "logs/anvio-profile-{sample}"
    benchmark:
        "benchmarks/anvio-profile-{sample}.time"
    params:
        bam=lambda w: "mapping/{sample}.reads.vs.contigs.bam".format(sample=w.sample),
        min_len=config.get("min_contig_length",2500)
    version:
        get_version("anvi-profile",
                    cmd_prefix="source activate {}\n"\
                               .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """source activate {config[anvio_env]}
           rm -rf anvio/profile-{wildcards.sample}
           anvi-profile -i {params.bam} -c contigs.db -S {wildcards.sample} \
            -o anvio/profile-{wildcards.sample} \
            --min-contig-length {params.min_len}"""

rule anvio_merge_profiles:
    """ Merge all the AnVio profiles """
    input:
        contig_status="anvio/status/contig-db",
        profiles=lambda w: expand("anvio/profile-{sample}/RUNINFO.cp",
                            sample=config['reads']),
    output:
        merged_samples_dir
    log:
        "logs/anvio-merged"
    benchmark:
        "benchmarks/anvio.merge.time"
    version:
        get_version("anvi-profile",
                    cmd_prefix="source activate {}\n"\
                               .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """source activate {config[anvio_env]}
           anvi-merge {input.profiles} -c contigs.db \
                -o anvio/samples-{config[samples_name]}-merged
           touch {output}"""

