"""
Collection of rules to create a sample databse for metagenomic
analysis in anvi'o
"""
config.setdefault('anvio_env','anvio')

config.setdefault('samples_name','all')
config.setdefault('sample_tree',{})
config['sample_tree'].setdefault('distance_metrics',['euclidean',])
config['sample_tree'].setdefault('coverage_metrics',\
                                 ['mean_coverage_Q2Q3_contigs'])

sample_db_file = "anvio/samples.{}.db".format(config['samples_name'])
if 'sample_metadata' in config:
    config.setdefault('outputs',set()).add(sample_db_file)

rule samples_db:
    """
    compile a table of metadata and some sorting options into a sample database
    """
    input:
        order="anvio/samples.orders.{samples}.tsv",
        data=config.get('sample_metadata','samples.dummy.tsv')
    output:
        "anvio/samples.{samples}.db"
    benchmark:
        "benchmarks/anvio.sample.db.time"
    version:
        get_version("anvi-gen-samples-info-database",
                    cmd_prefix="source activate {} \n"\
                                .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
        
    shell:
        """ source activate {config[anvio_env]}
            rm -f {output}
            anvi-gen-samples-info-database -D {input.data} -R {input.order} \
                -o {output}
        """

rule compile_sample_orders:
    """ collect newick trees into ordering file for samples """
    input:
        lambda w: expand(
         "anvio/sample_tree.{samples}.{coverage_metric}.{distance_metric}.newick",
         samples=w.samples,
         coverage_metric=config['sample_tree']['coverage_metrics'],
         distance_metric=config['sample_tree']['distance_metrics'])
    output:
        table="anvio/samples.orders.{samples}.tsv"
    run:
        lines = ["attributes\tbasic\tnewick",]
        for newick_file in input:
            with open(newick_file) as f:
                tree = f.read().strip()
            lines.append("coverage\t\t{}".format(tree))
        with open(output.table, 'w') as out:
            out.write("\n".join(lines))

rule cluster_samples:
    """ Use hierarchical clustering to build a dendrogram of samples """
    input:
        "anvio/sample_coverages.{samples}.{coverage_metric}.tsv"
    output:
        "anvio/sample_tree.{samples}.{coverage_metric}.{distance_metric}.newick"
    benchmark:
        "benchmarks/cluster_samples_{coverage_metric}_{distance_metric}.time"
    version:
        get_version("anvi-matrix-to-newick",
                    cmd_prefix="source activate {} \n"\
                                .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """ source activate {config[anvio_env]}
            anvi-matrix-to-newick {input} --transpose \
            --distance {wildcards.distance_metric} -o {output} """

rule sample_coverage_table:
    """ 
    Extract the desired metric from the merged sample db 
    
    We need to drop the last column (it's text, not data)
    """
    input:
        "anvio/status/samples-{samples}-merged"
    output:
        "anvio/sample_coverages.{samples}.{coverage_metric}.tsv"
    benchmark:
        "benchmarks/extract_sample_{coverage_metric}.time"
    version:
        get_version("anvi-matrix-to-newick",
                    cmd_prefix="source activate {} \n"\
                                .format(config['anvio_env']),
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """ source activate {config[anvio_env]}
            anvi-export-table {config[merged_samples_dir]}/PROFILE.db --table {wildcards.coverage_metric} -o {output}.raw
            perl -pe 's/\\t[^\\t]+$/\n/' {output}.raw > {output}
            rm {output}.raw
        """

