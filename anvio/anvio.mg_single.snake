"""
Python Makefile to prep a set of contigs and reads for AnVio per:
http://merenlab.org/2016/06/22/anvio-tutorial-v2/
"""
import os
import sys
snakefile_path=os.path.dirname(os.path.abspath(workflow.snakefile))
sys.path[0]=os.path.join(snakefile_path,'..','python')
from common import get_version, collect_sample_reads
configfile: "./anvio.yaml"

####
# Required conda packages:
#
# snakemake bwa prinseq pandas last samtools
# 
# also install centrifuge to conda env with:
#   make install prefix=$CONDA_PREFIX
# 
# also need a conda environment for AnVio 
#    (config['anvio_env'] defaults to "anvio")
###

######################
# Configuration
#
# Get sample reads from dir and pattern if not provided
collect_sample_reads(config)
#
# if we're using centrifuge, load dummy info in dbs
if 'centrifuge' in config:
    config.setdefault('dbs',{'RefSeq':{'type':'tax',
                                       'path':'/dummy'}})
# Configuration
######################

include: "anvio.contigs.snake"
include: "anvio.profiles.snake"
include: "../common/mapping_bwa.snake"
include: "../common/common.snake"

#print(config)


######################
# Rules

rule anvio_all:
    input:
        "contigs.fasta.stats",
        "anvio/status/contig-hmms",
        "anvio/status/contig-tax",
        "anvio/status/samples-merged",
        "anvio/status/contig-cogs"

rule fake_genes:
    """ create a symlink to the contig genes file with the proper extension, so the lastal rule will find it """
    input: "contigs.genes.ffn"
    output: temp("contigs.genes.fasta")
    shell: "ln -s {input} {output}"

rule anvio_merge_profiles:
    """ Merge all the AnVio profiles """
    input:
        profiles=expand("anvio/profile-{sample}/RUNINFO.cp",
                    sample=config['reads']),
        contigs_tax="anvio/status/contig-tax",
    output:
        "anvio/status/samples-merged"
    log:
        "logs/anvio-merged"
    benchmark:
        "benchmarks/anvio.merge.time"
    version:
        get_version("anvi-profile",
                    cmd_prefix="source activate anvio \n",
                    lines=[1,],
                    regular_expression=r'(\d.+)')
    shell:
        """source activate anvio
           anvi-merge {input.profiles} -c contigs.db -o anvio/samples-merged
           touch {output}"""


