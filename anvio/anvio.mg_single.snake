"""
Python Makefile to prep a set of contigs and reads for AnVio per:
http://merenlab.org/2016/06/22/anvio-tutorial-v2/
"""
import os
import sys
snakefile_path=os.path.dirname(os.path.abspath(workflow.snakefile))
sys.path[0]=os.path.join(snakefile_path,'..','python')
from common import get_version, collect_sample_reads
configfile: "./anvio.yaml"

include: "anvio.contigs.snake"
include: "anvio.profiles.snake"
include: "../common/mapping_bwa.snake"
include: "../common/common.snake"

####
# Required conda packages:
#
# snakemake bwa prinseq pandas last
# 
# also need a conda environment for AnVio 
#    (config['anvio_env'] defaults to "anvio")
###

######################
# Configuration
#
# Get sample reads from dir and pattern if not provided
collect_sample_reads(config)
#
# Configuration
######################

#print(config)


######################
# Rules

rule anvio_all:
    input:
        "contigs.fasta.stats",
        "anvio/status/contig-hmms",
        "anvio/status/contig-tax",
        "anvio/status/samples-merged",

rule anvio_contigs:
    """ Simplify the contig names so AnVio doesn't freak out """
    input:
        config.get('contig_fasta',"contigs.fasta")
    output:
        "contigs.fa"
    version:
        get_version('sed', lines=[1,])
    log:
        "logs/simplify-contig-names"
    benchmark:
        "benchmarks/simplify-contig-names.time"
    shell:
        'cat {input} | sed -r "s/^>(\S+)\s*.*$/>\\1/" > {output}'

rule fake_genes:
    """ create a symlink to the contig genes file with the proper extension, so the lastal rule will find it """
    input: "contigs.genes.ffn"
    output: temp("contigs.genes.fasta")
    shell: "ln -s {input} {output}"

rule fake_contigs:
    """ create a symlink to the contigs with the proper extension so that the mapping rules will pick it up """
    input: "contigs.fa"
    output: temp("contigs.fasta")
    shell: "ln -s {input} {output}"

rule anvio_merge_profiles:
    """ Merge all the AnVio profiles """
    input:
        profiles=expand("anvio/profile-{sample}/RUNINFO.cp",
                    sample=config['reads']),
        contigs_tax="anvio/status/contig-tax"
    output:
        "anvio/status/samples-merged"
    log:
        "logs/anvio-merged"
    benchmark:
        "benchmarks/anvio.merge.time"
    shell:
        """source activate anvio
           anvi-merge {input.profiles} -c contigs.db -o anvio/samples-merged
           touch {output}"""


