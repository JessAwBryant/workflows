from python import bbmap
from snakemake.utils import update_config
defaults={'bbduk':{'ram':'1g',
                   'adapter_flags':
                        'ktrim=r k=23 mink=11 hdist=1 tbo tpe tbo tpe',
                   'phix_flags': (
                        'k=27 hdist=1 qtrim=rl trimq=17 cardinality=t '
                        'mingc=0.05 maxgc=0.95'),
                   },
         }
update_config(config, defaults)

# look in config[system_paths][bbmap] and relative to bbduk.sh script
#  for adapter file folder (aka resources)
if 'resource_dir' not in config['bbduk']:
    config['bbduk']['resource_dir'] = bbmap.get_bbduk_resource_path(config)

rule trim_bbduk_adapters:
    """
    remove adapters with bbduk
    """
    input:
        "{prefix}.{suffix}"
    output:
        "{prefix}.noadapt.{suffix}"
    wildcard_constraints:
        suffix=r'(R[12]\.)?fastq'
    benchmark:
        "benchmarks/{prefix}.noadapt.fastq.time"
    log:
        "logs/{prefix}.noadapt.log"
    version:
        get_version('bbduk.sh',
                    lines=[1,],)
    params:
        adapter_file=config['bbduk']['resource_dir'] \
                        + "/adapters.fa"
    shell:
        "bbduk.sh -Xmx{config[bbduk][ram]} \
          in={input} out={output} \
          interleaved overwrite {config[bbduk][adapter_flags]} \
          ref={params.adapter_file} 2> {log}"

rule trim_bbduk_phix:
    """
    remove phiX with bbduk
    """
    input:
        "{prefix}.fastq"
    output:
        "{prefix}.bbduk_nophix.fastq",
        "{prefix}.bbduk_nophix.gchist.txt"
    benchmark:
        "benchmarks/{prefix}.bbduk_nophix.fastq.time"
    log:
        "logs/{prefix}.bbduk_nophix.log"
    version:
        get_version('bbduk.sh',
                    lines=[1,],)
    params:
        phiX_file=config['bbduk']['resource_dir'] \
                         + "/phix174_ill.ref.fa.gz",
    shell:
        "bbduk.sh -Xmx{config[bbduk][ram]} \
          in={input} out={output[0]} gchist={output[1]} \
          interleaved overwrite {config[bbduk][phix_flags]} \
          ref={params.phiX_file} 2> {log}"

